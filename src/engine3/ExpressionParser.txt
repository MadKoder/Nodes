{
    function makeInfixExpression(head, tail) {
        var result = {
            type : "CallExpression",
            func : {
                type : "Type",
                base : tail[0].operator,
                args : []
            },
            args : [head, tail[0].operand]
        };
        for (var i = 1; i < tail.length; i++) {
            result = {
                type : "CallExpression",
                func : {
                    type : "Type",
                    base : tail[i].operator,
                    args : []
                },
                args : [result, tail[i].operand]
            };
        };
        return result;
    }
}

Expression =
    AdditiveExpression

AdditiveExpression =
    head:MultiplicativeExpression _ tail:(operator:AdditiveOperator _ operand:MultiplicativeExpression _ {
        return {
            operator: operator,
            operand: operand
        };
    })+ {
        return makeInfixExpression(head, tail);
    }
    / MultiplicativeExpression

AdditiveOperator =
    "+" !("=") { return "+"; }
    / "-" !("=") { return "-"; }

MultiplicativeExpression = 
    head:ChainedExpression _ tail:(operator:MultiplicativeOperator _ operand:ChainedExpression _ {
        return {
            operator: operator,
            operand: operand
        };
    } )+ {
        return makeInfixExpression(head, tail);
    }
    / ChainedExpression

MultiplicativeOperator =
    operator:("*" / "/" / "%") !"=" { return operator; }

ChainedExpression =
    head:Atom _ operations:(ChainedOperation)+ {
        var result = operations[0];
        result[
            (result.type === "MemberExpression") ? 
                "struct" :
                "func"
        ] = head;
        for (var i = 1; i < operations.length; i++) {
            var operation = operations[i];
            operation[
                (operation.type === "MemberExpression") ? 
                    "struct" :
                    "func"
            ] = result;
            result = operation;
        }
        return result 
    }
    / Atom

ChainedOperation =
    CallExpression
    / MemberExpression

MemberExpression =
    "." _ field:Id _ {
        return {
            type: "MemberExpression",
            /*struct: struct,*/
            field: field
        };
    }

CallExpression "CallExpression" = 
    "(" _ args:ExpressionList _ ")" _
    {   
        return {
            type : "CallExpression",
            /*func : func,*/
            args : args
        };
    }

ExpressionList = 
    headExpr:Expression tailExpr:( _ "," _ (i:Expression) {return i})*
    {
        return [headExpr].concat(tailExpr);
    }

Atom = Id
    / "(" _ expr:Expression _ ")" _ {return expr;}

Id = 
    name:([a-zA-Z_][a-zA-Z0-9_]*) { 
        return {
            type : "Id",
            name : name.length == 1 ?
            name[0] :
            name[0] + name[1].join("")
        };
    }

_ = 
    (WhiteSpace)*

WhiteSpace "whitespace"
= [\t\v\f \u00A0\uFEFF]
/ Zs

// Separator, Space
Zs = [\u0020\u00A0\u1680\u180E\u2000\u2001\u2002\u2003\u2004\u2005\u2006\u2007\u2008\u2009\u200A\u202F\u205F\u3000]
